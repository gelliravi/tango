C  ********************************************************************
C  * SUBROUTINE TO PLOT GRAPHS OF SYSTEM VARIABLES
C  ********************************************************************
C
C  Copyright (c) 2013 IncSys (http://incsys.com)
C
C  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRENTY OF ANY KIND,
C  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRENTIES
C  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
C  NONINFRENGEMENT.  IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS
C  BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN
C  ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
C  CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
C  SOFTWARE.
C
      SUBROUTINE PLOT
      IMPLICIT NONE
      INCLUDE 'cblocks.inc'
      INTEGER I,J,LOC,LOG
      REAL RANGE,SCALE
      CHARACTER SYMBOL(6),PLUS,NAME(6,40),ALINE(132),BLANK
      DATA SYMBOL/'A','B','C','D','E','F'/
      DATA PLUS/'+'/,BLANK/' '/
      REAL VMAX(6),VMIN(6)
      REAL YVAL(11)
C  READ GRAPH NAMES AND MINIMUM AND MAXIMUM VALUES.
      DO 3 I=1,7
      IF(I .EQ. 7) GO TO 5
3     READ(5,1000,END=5) (NAME(I,J),J=1,40),VMIN(I),VMAX(I)
1000  FORMAT(40A1,F10.5,F10.5)
5     NVAR=I-1
      IF(NVAR .EQ. 0) RETURN
      WRITE(6,1060)
1060  FORMAT('1')
C  WRITE HEADINGS FOR EACH GRAPH.
      DO 20 I=1,NVAR
      RANGE=VMAX(I)-VMIN(I)
      LOG=ALOG10(RANGE)
      SCALE=1.0*10.0**LOG
      DO 10 J=1,11
10    YVAL(J)=(VMIN(I)+(J-1)*RANGE/10.0)/SCALE
      WRITE(6,1010) SYMBOL(I),(NAME(I,J),J=1,40),SCALE
1010  FORMAT('0',T12,A1,' - ',40A1,T93,'SCALE FACTOR = ',1PE7.1)
      WRITE(6,1020) (YVAL(J),J=1,11)
1020  FORMAT(' ',T10,11(F5.2,5X))
20    CONTINUE
C  WRITE Y AXIS.
      WRITE(6,1030) (PLUS,LOC=1,101)
1030  FORMAT('0    TIME  ',101A1)
C  PLOT GRAPHS.
      DO 50 J=1,NT
      DO 28 LOC=1,101
28    ALINE(LOC)=BLANK
      ALINE(1)=PLUS
      IF(MOD(J,10) .NE. 1) GO TO 32
C  INCLUDE GRID POINTS.
      DO 30 LOC=1,101,10
30    ALINE(LOC)=PLUS
32    CONTINUE
C  INCLUDE GRAPH POINTS.
      DO 35 I=1,NVAR
      LOC=(VAR(J,I)-VMIN(I))/(VMAX(I)-VMIN(I))*100.0+1.0
      IF(LOC .LT. 1) LOC=1
      IF(LOC .GT. 101) LOC=101
      ALINE(LOC)=SYMBOL(I)
35    CONTINUE
      IF(MOD(J,10) .EQ. 1) WRITE(6,1040) TYM(J),(ALINE(LOC),LOC=1,101)
1040  FORMAT(T4,F7.3,1X,101A1)
      IF(MOD(J,10) .NE. 1) WRITE(6,1045) (ALINE(LOC),LOC=1,101)
1045  FORMAT(T12,101A1)
50    CONTINUE
60    CONTINUE
      RETURN
      END


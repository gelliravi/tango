C  ********************************************************************
C  *                                                                  *
C  *             POWER SYSTEM DYNAMIC SIMULATION PROGRAM              *
C  *                                                                  *
C  ********************************************************************
C
C  Copyright (c) 2013 IncSys (http://incsys.com)
C
C  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRENTY OF ANY KIND,
C  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRENTIES
C  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
C  NONINFRENGEMENT.  IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS
C  BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN
C  ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
C  CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
C  SOFTWARE.
C
C  MAINLINE ROUTINE.
      PROGRAM PSDSIM
      IMPLICIT NONE
      INCLUDE 'cblocks.fpp'
      COMPLEX YFICT
      COMPLEX CMPLX,CONJG
      REAL C,TFIN,PT,QT,TOLD,VMAG,VARG,TPRINT
      INTEGER I,J,NGEN,NPRINT,NSTEP
      NT=0
      TIME=0.0
      TFIN=0.0
      NSTEP=0
      NPRINT=0
C  CLEAR INTEGRATOR ARRAYS
      DO 10 I=1,10
      DO 10 J=1,16
      PLUG(I,J)=0.0
      OUT(I,J)=0.0
      SAVE(I,J)=0.0
10    CONTINUE
      DO 12 I=1,10
      DO 12 J=1,20
12    PRTVAR(I,J)=0.0
      WRITE(6,990)
990   FORMAT('1',T39,'POWER  SYSTEM  RESEARCH  GROUP'//
     1T40,'UNIVERSITY  OF  SASKATCHEWAN'//
     2T32,'POWER  SYSTEM  DYNAMIC  SIMULATION  PROGRAM'//)
      READ(5,1000) NGEN,TSTEP,TPRINT
1000  FORMAT(I5,5X,2F10.4)
      WRITE(6,1005) NGEN,TSTEP,TPRINT
1005  FORMAT('0NO. OF GENERATORS',T20,I5/' TIME STEP',T20,F6.3/
     1' PRINT INTERVAL',T20,F6.3)
      WRITE(6,1008)
1008  FORMAT('0GENERATOR PARAMETERS')
C  READ GENERATOR PARAMETERS.
      DO 20 I=1,NGEN
      READ(5,1010)           PBASE(I),H(I),R(I),XL(I),XD(I),
     1XD1(I),XQ(I),XQ1(I),TD1(I),TQ1(I),DAMP(I),C1(I),C2(I)
1010  FORMAT(8F10.4)
      WRITE(6,1015) I,       PBASE(I),H(I),R(I),XL(I),XD(I),
     1XD1(I),XQ(I),XQ1(I),TD1(I),TQ1(I),DAMP(I),C1(I),C2(I)
1015  FORMAT(1X,I5,8G12.4/6X,8G12.4)
C  CONVERT DATA TO 100 MW BASE.
      C=100.0/PBASE(I)
      H(I)=H(I)/C
      R(I)=R(I)*C
      XL(I)=XL(I)*C
      XD(I)=XD(I)*C
      XD1(I)=XD1(I)*C
      XQ(I)=XQ(I)*C
      XQ1(I)=XQ1(I)*C
      DAMP(I)=DAMP(I)/C
20    CONTINUE
      WRITE(6,1018)
1018  FORMAT('0 EXCITATION SYSTEM PARAMETERS')
C  READ EXCITATION SYSTEM PARAMETERS.
      DO 30 I=1,NGEN
      READ(5,1020) (AVRPRM(I,J),J=1,16)
1020  FORMAT(8F10.4)
      WRITE(6,1025) I,(AVRPRM(I,J),J=1,16)
1025  FORMAT(1X,I5,8G12.4/6X,8G12.4)
30    CONTINUE
      WRITE(6,1028)
1028  FORMAT('0 TURBINE-GOVERNOR PARAMETERS')
C  READ TURBINE AND GOVERNOR PARAMETERS.
      DO 40 I=1,NGEN
      READ(5,1030) (TURPRM(I,J),J=1,16)
1030  FORMAT(8F10.4)
      WRITE(6,1035) I,(TURPRM(I,J),J=1,16)
1035  FORMAT(1X,I5,8G12.4/6X,8G12.4)
40    CONTINUE
      WRITE(6,1038)
1038  FORMAT('0INITIAL GENERATOR TERMINAL CONDITIONS'/
     1T3,'GEN',T15,'MW',T26,'MVAR',T36,'VOLTS',T46,'ANGLE')
C  READ CONDITIONS ON TERMINAL BUSES.
      DO 50 I=1,NGEN
      READ(5,1040)   PT,QT,VMAG,VARG
1040  FORMAT(2P2F10.4,0P2F10.4)
      WRITE(6,1045) I,PT,QT,VMAG,VARG
1045  FORMAT(1X,I5,5X,2P2F10.4,0P2F10.5)
      VARG=VARG*PI/180.0
      VT(I)=VMAG*CMPLX(COS(VARG),SIN(VARG))
      CT(I)=CONJG(CMPLX(PT,QT)/VT(I))
50    CONTINUE
C  CALL EQUIPMENT SUBROUTINES TO CALCULATE INITIAL CONDITIONS.
C  CALL STATMENTS MUST BE GENERATED BY USER.
C  ********************************************************************
      CALL GEN1IC(1)
      CALL GEN1IC(2)
      CALL AVR1IC(1)
C  ********************************************************************
C  LOOP HERE FOR EACH NEW NETWORK CONDINTION
70    CONTINUE
      TOLD=TFIN
C  READ THE CONTROL CARD.
      READ(5,1050) TFIN
1050  FORMAT(F10.4)
      IF(TFIN .EQ. 0.0) GO TO 150
      WRITE(6,1055) TOLD,TFIN
1055  FORMAT('0',T9,'TERMINAL ADMITTANCE MATRIX FROM',F8.3,' TO',F8.3,
     1' SECS.')
C  READ THE NEW ADMITTANCE MATRIX.
      DO 72 I=1,NGEN
      READ(5,1060) (Y(I,J),J=1,NGEN)
1060  FORMAT(8F10.4)
      WRITE(6,1065) (Y(I,J),J=1,NGEN)
1065  FORMAT((T9 ,8F10.4))
72    CONTINUE
      CALL MATRIX(NGEN)
      WRITE(6,1076)
1076  FORMAT('0')
C  LOOP HERE FOR EACH INTEGRATION STEP.
100   CONTINUE
C  SOLVE THE NETWORK.
      CALL NWSOL(NGEN)
C  CALL EQUPMENT SUBROUTINES TO CALCUALTE STATE VARIABLE DERIVATIVES.
C  CALL STATMENTS MUST BE GENERATED BY USER.
C  ********************************************************************
      CALL AVR1(1)
      CALL GEN1(1)
      CALL GEN1(2)
C  ********************************************************************
C  CHECK FOR OUTPUT.
      IF(NSTEP .EQ. 0) CALL OUTPUT(NGEN)
      IF(NPRINT*TSTEP .LT. TPRINT-.0001) GO TO 125
      CALL OUTPUT(NGEN)
      NPRINT=0
125   CONTINUE
C  PERFORM INTEGRATION STEP.
      CALL INT(NGEN)
      NSTEP=NSTEP+1
      TIME=NSTEP*TSTEP
      NPRINT=NPRINT+1
C  CHECK FOR NEW NETWORK CONDINTION.
      IF(TIME .LT. TFIN) GO TO 100
C  LOOP BACK FOR NEW NETWORK CONDITION.
      GO TO 70
C  COME HERE WHEN RUN IS COMPLETED
150   CONTINUE
      CALL PLOT
      STOP
      END


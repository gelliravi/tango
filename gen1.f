C  ********************************************************************
C  *  MODEL OF SYNCHRONOUS GENERATOR WITH FIELD WINDING IN D AXIS AND
C  *  DAMPER WINDING IN O AXIS
C  ********************************************************************
      SUBROUTINE GEN1(I)
      IMPLICIT NONE
      INCLUDE 'cblocks.inc'
      REAL CSAT(10)
      COMPLEX CONJG,CMPLX,EQD,CURR
      REAL ID,IQ,OME,DEL,EQ,ED,EAD,EAQ,EAT,OMF,PE,PL,QE
      REAL TD1S,TQ1S,XDS,XQS
      INTEGER I,J
C  ENTER HERE FOR EACH INTEGRATION STEP.
C  DEFINE INTEGRATOR OUTPUTS.
      OME=OUT(I,1)
      DEL=OUT(I,2)
      EQ=OUT(I,3)
      ED=OUT(I,4)
C  TRANSFORM CURRENT TO MACHINE REFERENCE.
      CURR=CT(I)*CMPLX(SIN(DEL),COS(DEL))
      ID=REAL(CURR)
      IQ=AIMAG(CURR)
C  CALCULATE GENERATOR OUTPUT PLUS LOSSES.
      PE=REAL(VT(I)*CONJG(CT(I)))
      QE=AIMAG(VT(I)*CONJG(CT(I)))
      PL=CABS(CURR)**2*R(I)
C  ADJUST REACTANCES AND TIME CONSTANTS TO ACCOUNT FOR SATURATION.
      XDS=CSAT(I)*XD(I)+(1.0-CSAT(I))*XL(I)
      XQS=CSAT(I)*XQ(I)+(1.0-CSAT(I))*XL(I)
      IF(XQ1(I) .EQ. XQ(I))XQS=XQ(I)
      TD1S=TD1(I)*(1.0-(1.0-CSAT(I))*(XD(I)-XD1(I))/(XD(I)-XL(I)))
      TQ1S=TQ1(I)*(1.0-(1.0-CSAT(I))*(XD(I)-XQ1(I))/(XQ(I)-XL(I)))
      EAQ=EQ-(XD1(I)-XL(I))*ID
      EAD=ED+(XQ1(I)-XL(I))*IQ
      IF(XQ1(I) .EQ. XQ(I))EAD=0.0
      EAT=SQRT(EAQ**2+EAD**2)
      CSAT(I)=1.0/(1.0+C1(I)*EXP(C2(I)*EAT))
C  DEFINE INTEGRATOR INPUTS.
C  SET UP PRINTOUT VARIABLES.
      PLUG(I,1)=(PM(I)-PE-PL-DAMP(I)*OME)/(2.0*H(I))
      PLUG(I,2)=377.0*OME
      PLUG(I,3)=(CSAT(I)*EF(I)-EQ-(XDS-XD1(I))*ID)/TD1S
      PLUG(I,4)=(-ED+(XQS -XQ1(I))*IQ)/TQ1S
      PRTVAR(I,1)=DEL*180.0/PI
      PRTVAR(I,2)=OMF
      PRTVAR(I,3)=EQ
      PRTVAR(I,4)=ED
      PRTVAR(I,5)=CABS(VT(I))
      PRTVAR(I,6)=PE*100.0/PBASE(I)
      PRTVAR(I,7)=QE*100.0/PBASE(I)
      PRTVAR(I,8)=EF(I)
      PRTVAR(I,9)=PM(I)*100.0/PBASE(I)
      PRTVAR(I,10)=CSAT(I)
      PRTVAR(I,11)=EAT
C  ENTER HERE TO CALCULATE INITIAL CONDITIONS.
      ENTRY GEN1IC(I)
      CSAT(I)=1.0
      DO 100 J=1,3
      XDS=CSAT(I)*XD(I)+(1.0-CSAT(I))*XL(I)
      XQS=CSAT(I)*XQ(I)+(1.0-CSAT(I))*XL(I)
      IF(XQ1(I) .EQ. XQ(I))XQS=XQ(I)
C  CALCULATE ANGLE OF GENERATOR Q AXIS.
      EQD=VT(I)+CMPLX(R(I),XQS)*CT(I)
      DEL=ATAN2(AIMAG(EQD),REAL(EQD))
C  TRANSFORM CURRENT ONTO GENERATOR REFERENCE.
      CURR=CT(I)*CMPLX(SIN(DEL),COS(DEL))
      ID=REAL(CURR)
      IQ=AIMAG(CURR)
      EQ=CABS(EQD)-(XQS -XD1(I))*ID
      EF(I)=(EQ+(XDS-XD1(I))*ID)/CSAT(I)
      ED=(XQS -XQ1(I))*IQ
      EAQ=EQ-(XD1(I)-XL(I))*ID
      EAD=ED+(XQ1(I)-XL(I))*IQ
      IF(XQ1(I) .EQ. XQ(I))EAD=0.0
      EAT=SQRT(EAQ**2+EAD**2)
      CSAT(I)=1.0/(1.0+C1(I)*EXP(C2(I)*EAT))
100   CONTINUE
      PE=REAL(VT(I)*CONJG(CT(I)))+CABS(CURR)**2*R(I)
      PM(I)=PE
      OUT(I,1)=0.0
      OUT(I,2)=DEL
      OUT(I,3)=EQ
      OUT(I,4)=ED
      RETURN
      END

